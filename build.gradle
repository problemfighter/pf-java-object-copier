
plugins {
    id 'java'
    id 'signing'
    id 'maven-publish'
}


repositories {
    mavenCentral()
    jcenter()
}

group 'com.problemfighter.java.oc'
version '1.0.1'

dependencies {
    compile group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compile
    options.addBooleanOption('html5', true)
}

task makeDoc {
    dependsOn(javadoc)
    doLast {
        def docDirectory = file("docs")
        if (!docDirectory.exists()){
            docDirectory.mkdirs();
        }
        delete "docs/javadoc"
        copy{
            from "$buildDir/docs/javadoc"
            into 'docs/javadoc'
        }
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

Boolean isPublish = false
if (isPublish) {

    publishing {

        repositories {
            maven {
                credentials {
                    username = (project.hasProperty('ossrhUsername') ? project.property('ossrhUsername') : "hmtmcse" )
                    password = (project.hasProperty('ossrhPassword') ? project.property('ossrhPassword') : "123" )
                }
                url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            }
        }

        publications {
            mavenJava(MavenPublication) {
                artifactId = 'pf-java-object-copier'
                groupId = 'com.problemfighter.java.oc'
                from components.java
                artifact sourcesJar
                artifact javadocJar
                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }
                pom {
                    name = 'Problem Fighter Java Object Copier'
                    description = 'Problem Fighter Java Object Copier'
                    url = 'http://www.hmtmcse.com/opensource/java/oc'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'hmtmcse'
                            name = 'H.M Touhid Mia'
                            email = 'hmtm.cse@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/problemfighter/pf-java-object-copier.git'
                        developerConnection = 'scm:git:https://github.com/problemfighter/pf-java-object-copier.git'
                        url = 'https://github.com/problemfighter/pf-java-object-copier'
                    }
                }
            }
        }
    }

    signing {
        sign publishing.publications.mavenJava
    }
}
